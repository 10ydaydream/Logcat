apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {

    resourcePrefix "logcat_"

    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 86
        versionName "8.6"
    }
}

dependencies {
    // 权限请求框架：https://github.com/getActivity/XXPermissions
    implementation 'com.hjq:xxpermissions:8.8'
    // 悬浮窗框架：https://github.com/getActivity/XToast
    implementation 'com.hjq:xtoast:6.0'
}

publish {
    userOrg = 'getactivity'
    groupId = 'com.hjq'
    artifactId = 'logcat'
    version = '8.6'
    description = 'This is to help developers quickly see the logging framework they want'
    website = "https://github.com/getActivity/Logcat"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}